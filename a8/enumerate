#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;

my %args;

my %media = (
    xegs => {window => '$8000', pages => 32},
    megamax => {window => '$8000', pages => 64},
    atarimax => {window => '$A000', pages => 32},
    megacart => {window => '$8000', pages => 64},
    sic => {window => '$8000', pages => 64},
    thecart => {window => '$A000', pages => 32},
    ram => {window => '$4000', pages => 64},
    emulator => {window => '$4000', pages => 128},
);

my @methods = qw(pcm44 pcm4 pwm covox);

sub usage {
    die "Usage: enumerate [-media X] [-method X] [-channels X] [-period X]\n";
}

sub set {
    my ($name, $value) = @_;
    if ($args{set}{$name}++) {
        push @{$args{$name}}, $value;
    } else {
        $args{$name} = [$value];
    }
}
sub main {
    %args = (
        media => [sort keys %media],
        method => [@methods],
        channels => [qw(mono stereo)],
        period => [qw(34 37 48 105 210)],
    );
    GetOptions(
        "media=s@" => \&set,
        "method=s@" => \&set,
        "channels=s@" => \&set,
        "period=s@" => \&set,
    ) or usage();
    my $player = `cat player.asm.pl`;
    for my $media (sort @{$args{media}}) {
        my $cart = $media =~ /ram|emulator/ ? 0 : 1;
	for my $method (sort @{$args{method}}) {
	    for my $channels (sort @{$args{channels}}) {
		for my $period (reverse @{$args{period}}) {
                    my $fn = "player-$media-$method-$channels-$period.asm.pl";
                    open my $fh, ">players/$fn" or die "ERROR: Cannot open $fn: $!\n";
		    print $fh "\$media='$media'; \$$media=1; \$cart=$cart; \$$method=1; \$$channels=1; \$period=$period;";
                    for my $var (sort keys %{$media{$media}}) {
                        my $val = $media{$media}{$var};
                        if ($val =~ /\$/) {
                            print $fh " \$$var='$media{$media}{$var}';";
                        } else {
                            print $fh " \$$var=$media{$media}{$var};";
                        }
                    }
                    print $fh "\n";
                    print $fh $player;
                    close $fh;
                    my $asm = $fn;
                    $asm =~ s/\.pl$//;
                    my $lab = $fn;
                    $lab =~ s/\.asm\.pl$/.lab/;
                    my $lst = $fn;
                    $lst =~ s/\.asm\.pl$/.lst/;
                    warn "INFO: Assembling $asm...\n";
                    system "cd players; perl $fn > $asm && " .
                        "xasm /t:$lab /l:$lst $asm";
		}
	    }
	}
    }
}

main();
